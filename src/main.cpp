#include <Arduino.h>
#include <Adafruit_LiquidCrystal.h>
#include <Keypad.h>

const int BUZZ = 4;

Adafruit_LiquidCrystal lcdRoulet(0);
Adafruit_LiquidCrystal lcdMoney(1);

const byte ROWS = 4;
const byte COLS = 4;

char keys[ROWS][COLS] = {
    {'1', '2', '3', 'A'},
    {'4', '5', '6', 'B'},
    {'7', '8', '9', 'C'},
    {'*', '0', '#', 'D'}};

byte rowPins[ROWS] = {13, 12, 11, 10};
byte colPins[COLS] = {9, 8, 7, 6};

Keypad keypad = Keypad(makeKeymap(keys), rowPins, colPins, ROWS, COLS);

bool gameOver = false;
int balance = 0;
int bet = 0;
char symbols[] = {'7', '$', '*', '#', '@', '!', '+'};
char r1, r2, r3, r4;

bool changeMode = true;
int cursorCol = 0;
int cursorRow = 0;

void setCursor(int col, int row)
{
  cursorCol = col;
  cursorRow = row;
  lcdMoney.setCursor(col, row);
}
void lcdPrint(String text)
{
  lcdMoney.print(text);
  cursorCol = cursorCol + text.length();
  if (cursorCol >= 16)
  {
    cursorCol = cursorCol - 16;
    cursorRow = cursorRow + 1;
  }
}
void showGameOver()
{
  gameOver = true;
  changeMode = false;
  setCursor(0, 0);
  lcdRoulet.clear();
  lcdRoulet.setCursor(0, 0);
  lcdRoulet.print("   GAME OVER!  ");
  lcdRoulet.setCursor(0, 1);
  lcdRoulet.print(" Press D Reset! ");
  lcdMoney.clear();
  lcdMoney.setCursor(0, 0);
  lcdMoney.print(" You are broke!");
  lcdMoney.setCursor(0, 1);
  lcdMoney.print(" Balance: $0.00 ");
}

void underlineFlick()
{
  static unsigned long lastFlick = 0;
  static bool underlineVisible = true;
  static bool lastChangeMode = false;
  static int lastCol = cursorCol;
  static int lastRow = cursorRow;

  if (lastCol != cursorCol || lastRow != cursorRow)
  {
    if (lastCol >= cursorCol)
    {
      lcdMoney.setCursor(lastCol, lastRow);
      lcdMoney.print(" ");
    }
    lastCol = cursorCol;
    lastRow = cursorRow;
    underlineVisible = true;
    lastFlick = 0;
  }

  if (changeMode && !lastChangeMode)
  {
    underlineVisible = true;
    lastFlick = 0;
  }
  lastChangeMode = changeMode;

  if (changeMode)
  {
    if (millis() - lastFlick >= 500)
    {
      lastFlick = millis();
      lcdMoney.setCursor(cursorCol, cursorRow);
      if (underlineVisible)
      {
        lcdMoney.print("_ ");
      }
      else
      {
        lcdMoney.print("  ");
      }
      lcdMoney.setCursor(cursorCol, cursorRow);
      underlineVisible = !underlineVisible;
    }
  }
  if (!changeMode)
  {
    lcdMoney.setCursor(cursorCol, cursorRow);
    lcdMoney.print(" ");
  }
}

void BalanceUpdate(int multiplier)
{
  long winnings = (long)bet * multiplier;
  long NewBalance = (long)balance + winnings;
  if (NewBalance <= 32767)
  {
    balance = NewBalance;
  }
  else
  {
    balance = 32767;
  }
  lcdMoney.setCursor(8, 1);
  lcdMoney.print("        ");
  lcdMoney.setCursor(8, 1);
  lcdMoney.print(balance);
  lcdMoney.setCursor(cursorCol, cursorRow);
}
void showWelcome()
{
  // Right lcd
  lcdRoulet.clear();
  lcdRoulet.setCursor(0, 0);
  lcdRoulet.print("  SLOT MACHINE  ");
  lcdRoulet.setCursor(0, 1);
  lcdRoulet.print(" Press to Play! ");
  // Left lcd
  lcdMoney.clear();
  setCursor(0, 0);
  lcdPrint("Bet:");
  setCursor(0, 1);
  lcdPrint("Balance:");
}
void resetGame()
{
  gameOver = false;
  balance = 0;
  bet = 0;
  changeMode = true;
  cursorCol = 0;
  cursorRow = 0;
  underlineFlick();
  setup();
}

void setup()
{
  // lcd
  lcdRoulet.begin(16, 2);
  lcdMoney.begin(16, 2);
  // piezo
  pinMode(BUZZ, OUTPUT);
  // Other
  randomSeed(analogRead(A0));
  showWelcome();
}
void spinReels()
{
  lcdRoulet.setCursor(0, 0);
  lcdRoulet.print(" -- Spinning -- ");
  for (int i = 0; i < 9; i++)
  {
    lcdRoulet.setCursor(0, 1);
    lcdRoulet.print("  [");
    r1 = symbols[random(0, 7)];
    lcdRoulet.print(r1);
    lcdRoulet.print("][");
    r2 = symbols[random(0, 7)];
    lcdRoulet.print(r2);
    lcdRoulet.print("][");
    r3 = symbols[random(0, 7)];
    lcdRoulet.print(r3);
    lcdRoulet.print("][");
    r4 = symbols[random(0, 7)];
    lcdRoulet.print(r4);
    lcdRoulet.print("]  ");
    tone(BUZZ, 500, 50);
    delay(100);
  }
  lcdRoulet.setCursor(0, 0);
  lcdRoulet.print(" --  Result  -- ");
}
void loop()
{
  char key = keypad.getKey();
  if (key)
  {
    if (gameOver)
    {
      if (key == 'D')
      {
        resetGame();
      }
      return;
    }
    switch (key)
    {
    case '*':
      if (!changeMode)
      {
        spinReels();
        if (r1 == r2 || r2 == r3 || r3 == r4)
        {
          if ((r1 == r2 && r2 == r3) || (r2 == r3 && r2 == r4))
          {
            if (r1 == r2 && r2 == r3 && r3 == r4)
            {
              for (int i = 0; i < 3; i++)
              {
                tone(BUZZ, 2000, 100);
              }
              BalanceUpdate(10);
            }
            else
            {
              tone(BUZZ, 1500, 100);
              BalanceUpdate(5);
            }
          }
          else
          {
            BalanceUpdate(2);
          }
        }
        else
        {
          balance -= bet;
          lcdMoney.setCursor(8, 1);
          lcdMoney.print("        ");
          lcdMoney.setCursor(8, 1);
          lcdMoney.print(balance);
          if (bet > balance)
          {
            bet = balance;
            lcdMoney.setCursor(4, 0);
            lcdMoney.print("          ");
            lcdMoney.setCursor(4, 0);
            lcdMoney.print(bet);
          }
          if (balance <= 0)
          {
            showGameOver();
          }
          lcdMoney.setCursor(cursorCol, cursorRow);
        }
      }
      break;
    case 'A':
      if (changeMode)
      {
        setCursor(4, 0);
        lcdPrint(String(bet));
      }
      break;
    case 'B':
      if (balance > 0)
      {
        bet = balance;
        setCursor(4, 0);
        lcdPrint((String)bet);
      }
      break;
    case 'C':
      changeMode = !changeMode;
      break;
    case 'D':
      resetGame();
      break;
    case '#':
      if (changeMode)
      {
        if (cursorRow == 0)
        {
          setCursor(4, cursorRow);
          bet = bet / 10;
          lcdPrint(String(bet));
        }
        else if (cursorRow == 1)
        {
          setCursor(8, cursorRow);
          balance = balance / 10;
          lcdPrint(String(balance));
        }
      }
      break;
    default:
      if (key >= '0' && key <= '9' && changeMode)
      {
        int digit = key - '0';
        if (cursorRow == 0)
        {
          setCursor(4, cursorRow);
          long NewBet = (long)bet * 10 + digit;
          if (NewBet <= 32767 && NewBet <= balance)
          {
            bet = NewBet;
          }
          lcdPrint(String(bet));
        }
        else if (cursorRow == 1)
        {
          setCursor(8, cursorRow);
          long NewBalance = (long)balance * 10 + digit;
          if (NewBalance <= 32767)
          {
            balance = NewBalance;
          }
          lcdPrint(String(balance));
        }
      }
      break;
    }
  }
  if (!gameOver)
  {
    underlineFlick();
  }
}